import com.playground.Libs

import java.util.regex.Matcher
import java.util.regex.Pattern

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

Pattern patternOfUnitTestTask = Pattern.compile("^test(.+?)(Minified)?(?:Debug|Release)")

kapt {
    correctErrorTypes = true
}

androidExtensions {
    experimental = true
}


android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.playground.mvrx"
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.playground.base.TestInstrumentationRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets.all { sourceSet ->
        Matcher testTaskMatcher = patternOfUnitTestTask.matcher(sourceSet.name)
        if (testTaskMatcher.find()) {
            def variantName = testTaskMatcher.group(1)
            println "Added androidTest sourceSets to $sourceSet.name (variant name = ${variantName}"
            sourceSet.java.srcDirs += "src/androidTest${variantName}/java"
            sourceSet.kotlin.srcDirs += "src/androidTest${variantName}/java"
        }
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'

        test {
            debug.assets.srcDirs += files("$projectDir/schemas".toString())
            debug.assets.srcDirs += files("$projectDir/assets".toString())
            debug.assets.srcDirs += files("$projectDir/raw".toString())

            java.srcDirs += 'src/androidTest/java'
            kotlin.srcDirs += 'src/androidTest/java'
            java.srcDirs += sharedTestDir
            kotlin.srcDirs += sharedTestDir

            test.resources.srcDirs += 'src/test/resources'
        }

        androidTest {
            assets.srcDirs += files("$projectDir/schemas".toString())
            test.resources.srcDirs += 'src/test/resources'
            resources.srcDirs += ['src/test/resources']
            java.srcDirs += sharedTestDir
            kotlin.srcDirs += sharedTestDir
        }
    }

    lintOptions {
        // Disable lintVital. Not needed since lint is run on CI
        checkReleaseBuilds false
        // Allow lint to check dependencies
        checkDependencies true
        // Ignore any tests
        ignoreTestSources true

        disable 'GradleCompatible', 'FontValidationError'
    }


    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            include '**/*Tests.class'
            include '**/*Test.class'

            useJUnit {
                //With this category we can annotate tests will not run on JVM
                //due conflicts or other Robolectric Issue, but can run without any issue on Espresso or
                //normal device.
                excludeCategories 'com.playground.utils.DoNotRunOnJvm'
            }
            ignoreFailures false

            // use afterTest to listen to the test execution results
            afterTest { descriptor, result ->
                println "Executing test for ${descriptor.name} with result: ${result.resultType}"
            }
            systemProperty 'robolectric.logging.enabled', 'true'

            jvmArgs '-noverify'
            maxHeapSize = "2g"
        }
    }

    packagingOptions {
        // Exclude AndroidX version files
        exclude 'META-INF/*.version'
        // Exclude consumer proguard files
        exclude 'META-INF/proguard/*'
        // Exclude the Firebase/Fabric/other random properties files
        exclude '/*.properties'
        exclude 'fabric/*.properties'
        exclude 'META-INF/*.properties'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation Libs.Kotlin.stdlib
    implementation Libs.Coroutines.core
    implementation Libs.Coroutines.rx2
    implementation Libs.Coroutines.android

    implementation Libs.Google.gson

    implementation Libs.AndroidX.Lifecycle.extensions

    implementation Libs.AndroidX.appcompat
    implementation Libs.AndroidX.recyclerview
    implementation Libs.AndroidX.constraintlayout
    implementation Libs.AndroidX.coreKtx
    implementation Libs.AndroidX.Fragment.fragmentKtx

    implementation Libs.AndroidX.Navigation.runtime
    implementation Libs.AndroidX.Navigation.fragment
    implementation Libs.AndroidX.Navigation.ui

    implementation Libs.MvRx.core

    implementation Libs.RxJava.rxJava
    implementation Libs.RxJava.rxKotlin
    implementation Libs.RxJava.rxAndroid

    implementation Libs.Retrofit.moshiConverter
    implementation Libs.Retrofit.retrofit_rxjava_adapter

    implementation Libs.Google.material

    implementation Libs.Moshi.moshi
    implementation Libs.Moshi.moshi_kotlin
    kapt Libs.Moshi.moshi_kotlin_code_gen

    implementation Libs.Koin.koin
    implementation Libs.Koin.koinViewModel
    implementation Libs.Koin.koinScope

    implementation Libs.OkHttp.loggingInterceptor

    implementation Libs.Epoxy.epoxy
    implementation Libs.Epoxy.paging
    implementation Libs.Epoxy.dataBinding
    kapt Libs.Epoxy.processor

    implementation Libs.AndroidX.Room.runtime
    implementation Libs.AndroidX.Room.rxjava2
    kapt Libs.AndroidX.Room.compiler

    implementation Libs.AndroidX.Test.espressoIdlingResources

    testImplementation Libs.junit
    testImplementation Libs.robolectric
    testImplementation Libs.Mockito.core
    testImplementation Libs.AndroidX.Test.core
    testImplementation Libs.AndroidX.Test.runner
    testImplementation Libs.AndroidX.Test.rules
    testImplementation Libs.AndroidX.Test.espressoCore
    testImplementation Libs.AndroidX.Test.thruth
    testImplementation Libs.MvRx.test
    testImplementation Libs.AndroidX.Room.test
    testImplementation Libs.MockWebServer.webServer
    testImplementation Libs.AndroidX.Test.espressoCore
    testImplementation Libs.AndroidX.Test.espressoContrib
    testImplementation Libs.AndroidX.Test.espressoIdlingResources
    testImplementation Libs.AndroidX.Test.junitAssertions
    testImplementation Libs.AndroidX.Test.archCore

    //Instrumentation tests
    androidTestImplementation Libs.AndroidX.Test.core
    androidTestImplementation Libs.AndroidX.Test.runner
    androidTestImplementation Libs.AndroidX.Test.rules
    androidTestImplementation Libs.AndroidX.Test.archCore
    androidTestImplementation Libs.AndroidX.Test.junitAssertions
    androidTestImplementation Libs.AndroidX.Test.thruth
    androidTestImplementation Libs.AndroidX.Test.espressoCore
    androidTestImplementation Libs.AndroidX.Test.espressoContrib
    androidTestImplementation Libs.AndroidX.Test.espressoIdlingResources

    androidTestImplementation Libs.MockWebServer.webServer
}